plugins {
    id 'application'
    id 'org.panteleyev.jpackageplugin' version '1.7.3'
}

group = 'com.pythys'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.trilarion:java-vorbis-support:1.2.1'
    implementation 'com.thoughtworks.xstream:xstream:1.4.16'
    implementation 'org.beanshell:bsh:2.0b5'
}

application {
    mainClass.set('org.spicetrade.Game')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.addAll(['-Xlint:unchecked', '-Xlint:deprecation'])
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    manifest {
        attributes(
            'Built-By': 'Taher Alkhateeb',
            'Main-Class': 'org.spicetrade.Game',
            'Implementation-Title': 'Spice Trade',
            'Implementation-Vendor': 'pythys.com',
            'Implementation-URL': 'https://www.pythys.com'
        )
    }
    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

import org.panteleyev.jpackage.ImageType

jpackage {
    input = file("$buildDir/install/spicetrade")
    appName = 'SpiceTrade'
    appVersion = project.version
    mainClass = 'org.spicetrade.Game'
    mainJar = "$buildDir/libs/spicetrade.jar"
    javaOptions = ['-Xmx512m']
    destination = layout.buildDirectory.dir("dist")

    windows {
        type        = ImageType.MSI
        winMenu     = true
        winShortcut = true
        licenseFile = file('LICENSE')
    }
    mac {
        type        = ImageType.PKG
        icon        = layout.projectDirectory.file('images/spicetrade_logo.gif')
        macAppStore = false
        licenseFile = file('LICENSE')
    }
    linux {
        type             = ImageType.APP_IMAGE
    }
}
tasks.jpackage.dependsOn installDist
