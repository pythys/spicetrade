buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.7.2'
    }
}
apply plugin: 'application'
apply plugin: 'io.github.fvarrui.javapackager.plugin'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.github.trilarion:java-vorbis-support:1.2.1'
    implementation 'com.thoughtworks.xstream:xstream:1.4.16'
    implementation 'org.beanshell:bsh:2.0b5'
}

mainClassName = 'org.spicetrade.Game'

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked"
    options.compilerArgs << "-Xlint:deprecation"
}

tasks.withType(Jar) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    manifest {
        attributes 'Built-By': 'Taher Alkhateeb'
        attributes 'Main-Class': 'org.spicetrade.Game'
        attributes 'Implementation-Title': 'Spice Trade'
        attributes 'Implementation-Version': '2.0'
        attributes 'Implementation-Vendor': 'pythys.com'
        attributes 'Implementation-URL': 'https://www.pythys.com'
    }
    from {
        configurations.runtimeClasspath.collect {it.isDirectory() ? it : zipTree(it)}
    }
}

javapackager {
    mainClass = mainClassName
    bundleJre = true
    generateInstaller = true
}

task copyProps(type: Copy) {
    from 'spicetrade.properties'
    into 'build/spicetrade'
}

task packageLinux(type: io.github.fvarrui.javapackager.gradle.PackageTask) {
    dependsOn build
    platform = 'linux'
    createTarball = true
    jrePath = file(System.properties['java.home'])
    finalizedBy copyProps
}
task packageMac(type: io.github.fvarrui.javapackager.gradle.PackageTask) {
    dependsOn build
    platform = 'mac'
    createTarball = true
    finalizedBy copyProps
}
task packageWindows(type: io.github.fvarrui.javapackager.gradle.PackageTask) {
    dependsOn build
    platform = 'windows'
    createZipball = true
    finalizedBy copyProps
}

task packageAll(dependsOn: [ packageLinux, packageMac, packageWindows ])
